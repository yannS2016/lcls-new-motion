<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_MotionStage" Id="{27a9489d-ed3b-4885-89cd-bbc7b70f9440}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'call_after_init'}
FUNCTION_BLOCK FB_MotionStage
VAR
    iMotionLogger : I_MotionLogger;
    {attribute 'no_copy'}
    fbAxisRef : REFERENCE TO AXIS_REF;
    {attribute 'no_copy'}
    stMotionEpicsItf : REFERENCE TO ST_MotionEpicsItf;
    iPersistentDataStorage : I_PersistentDataStorage;

    //Brakes : brake fb participate in sync comm with fbMotionDrive, fbMotionAxis and fbMotionInterlocks
    fbBrake : FB_Brake := (BrakeMode := E_StageBrakeMode.IF_MOVING);
    // common interlocks
    fbMotionInterlocksCommon : FB_MotionInterlocksCommon(iMotionLogger := iMotionLogger);
    // stage with Limit switches
    fbLimSwStatus : FB_LimSwStatus();
    fbMotionInterlocksLimSw : FB_MotionInterlocksLimSw ( iMotionInterlocksCommon:=fbMotionInterlocksCommon, iLimSwStatus:=fbLimSwStatus);

    fbHomeNC : FB_HomeNC(AxisRef:=fbAxisRef, iMotionLogger:=iMotionLogger, iLimSwStatus:=fbLimSwStatus);;
    fbHaltNC : FB_HaltNC(AxisRef:=fbAxisRef);
    fbPowerNC : FB_PowerNC(AxisRef:=fbAxisRef);
    fbResetNC : FB_ResetNC(AxisRef:=fbAxisRef);
    fbMoveAbsoluteNC : FB_MoveAbsoluteNC(AxisRef:=fbAxisRef);
    fbAxisParametersNC: FB_AxisParametersNC(AxisRef:=fbAxisRef, iPersistentDataStorage := iPersistentDataStorage);
    fbAxisStatusNC : FB_AxisStatusNC(AxisRef:=fbAxisRef);

    fbBacklashCompensation : FB_BacklashCompensation(AxisRef:=fbAxisRef);
    (*The axis object has a interlock reference in this instance: this would make calling method
      to transfer power status from interlock interface override the internal passing of power enable status
      the powerenables() is provided in case the interlock status were passed via the drive
      nonetheless passing via this object is more flexible
    *)
    fbMotionAxisNC : FB_MotionAxisNC( 	iHome := fbHomeNC,
                                        iHalt := fbHaltNC,
                                        iPower := fbPowerNC,
                                        iReset := fbResetNC,
                                        iMoveAbsolute := fbMoveAbsoluteNC,
                                        iAxisParameters:= fbAxisParametersNC,
                                        iAxisStatus := fbAxisStatusNC,
                                        iBacklashCompensation :=fbBacklashCompensation,
                                    );
  //
   fbMotionDrive: FB_MotionDrive ( stMotionEpicsItf := stMotionEpicsItf,
                                   iBrake:=fbBrake,
                                   iMotionLogger:=iMotionLogger,
                                   iMotionInterlocks:=fbMotionInterlocksLimSw,
                                   iMotionAxis := fbMotionAxisNC
                                   ) ;
    // Drive Name
    sName : T_MaxString;
    //eEnableMode : ENUM_StageEnableMode;

END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
fbMotionInterlocksCommon();
fbLimSwStatus();
fbMotionInterlocksLimSw();
fbResetNC();
fbHaltNC();
fbPowerNC();
fbHomeNC();
fbBacklashCompensation();
fbMoveAbsoluteNC();
fbAxisParametersNC();
fbAxisStatusNC();
fbMotionAxisNC();
fbAxisParametersNC();
fbAxisStatusNC();
fbMotionDrive();
fbBrake();
// fbMotionLogger();
// fbPersistentDataStorage();]]></ST>
    </Implementation>
    <Property Name="BrakeMode" Id="{7b563b76-fecc-4885-824b-f91a0bc21af3}">
      <Declaration><![CDATA[{warning 'Add property implementation'}
PROPERTY BrakeMode : E_StageBrakeMode
]]></Declaration>
      <Get Name="Get" Id="{0f26607f-cf71-44b1-9984-79929f233e16}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[BrakeMode := fbBrake.BrakeMode;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{15a41f16-798f-4b50-9250-e347873647b8}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[fbBrake.BrakeMode := BrakeMode;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="CallAfterInit" Id="{e7a5e1c3-8324-4257-a302-ab714cb009e4}">
      <Declaration><![CDATA[{attribute 'call_after_init'}
METHOD PRIVATE CallAfterInit]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbMotionInterlocksCommon.CallAfterInit(iMotionLogger := THIS^.iMotionLogger);
//fbLimSwStatus.CallAfterInit(iMotionLogger := THIS^.iMotionLogger);
fbMotionInterlocksLimSw.CallAfterInit(iMotionInterlocksCommon := THIS^.fbMotionInterlocksCommon,
                                      iLimSwStatus := THIS^.fbLimSwStatus);
fbHomeNC.CallAfterInit(AxisRef := THIS^.fbAxisRef,
                       iMotionLogger := THIS^.iMotionLogger,
                       iLimSwStatus := THIS^.fbLimSwStatus);
fbHaltNC.CallAfterInit(AxisRef := THIS^.fbAxisRef);
fbPowerNC.CallAfterInit(AxisRef := THIS^.fbAxisRef);
fbResetNC.CallAfterInit(AxisRef := THIS^.fbAxisRef);
fbMoveAbsoluteNC.CallAfterInit(AxisRef := THIS^.fbAxisRef);
fbAxisParametersNC.CallAfterInit(AxisRef := THIS^.fbAxisRef,
                                 iPersistentDataStorage := THIS^.iPersistentDataStorage);
fbAxisStatusNC.CallAfterInit(AxisRef := THIS^.fbAxisRef);
fbBacklashCompensation.CallAfterInit(AxisRef := THIS^.fbAxisRef);
fbMotionAxisNC.CallAfterInit(iHome := THIS^.fbHomeNC,
                             iHalt := THIS^.fbHaltNC,
                             iPower := THIS^.fbPowerNC,
                             iReset := THIS^.fbResetNC,
                             iMoveAbsolute := THIS^.fbMoveAbsoluteNC,
                             iAxisParameters:= THIS^.fbAxisParametersNC,
                             iAxisStatus := THIS^.fbAxisStatusNC,
                             iBacklashCompensation := THIS^.fbBacklashCompensation);
fbMotionDrive.CallAfterInit( stMotionEpicsItf:= THIS^.stMotionEpicsItf,
                             iBrake:=THIS^.fbBrake,
                             iMotionInterlocks:=THIS^.fbMotionInterlocksLimSw,
                             iMotionLogger:=THIS^.iMotionLogger,
                             iMotionAxis:=THIS^.fbMotionAxisNC );

THIS^.stMotionEpicsItf.sName := THIS^.sName;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="EnableMode" Id="{d2517567-d217-491d-9b68-9264f829d81e}">
      <Declaration><![CDATA[PROPERTY EnableMode : ENUM_StageEnableMode]]></Declaration>
      <Get Name="Get" Id="{71e16d1d-1779-46bd-acad-1d5698e3b457}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[EnableMode := THIS^.fbMotionDrive.EnableMode;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{1782045b-083a-4984-8c31-ce01cd3f2942}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[THIS^.fbMotionDrive.EnableMode := EnableMode;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="FB_Init" Id="{fe4c9de1-abf0-4550-ae13-2cd56df378be}">
      <Declaration><![CDATA[//FB_Init is always available implicitly and it is used primarily for initialization.
//The return value is not evaluated. For a specific influence, you can also declare the
//methods explicitly and provide additional code there with the standard initialization
//code. You can evaluate the return value.
METHOD FB_Init: BOOL
VAR_INPUT
    bInitRetains: BOOL; // TRUE: the retain variables are initialized (reset warm / reset cold)
    bInCopyCode: BOOL;  // TRUE: the instance will be copied to the copy code afterward (online change)
    iMotionLogger : I_MotionLogger;
    fbAxisRef : REFERENCE TO AXIS_REF;
    stMotionEpicsItf : REFERENCE TO ST_MotionEpicsItf;
    iPersistentDataStorage : I_PersistentDataStorage;
    sName : T_MaxString;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.iMotionLogger := iMotionLogger;
THIS^.fbAxisRef REF= fbAxisRef;
THIS^.iPersistentDataStorage := iPersistentDataStorage;
THIS^.stMotionEpicsItf REF=stMotionEpicsItf;
THIS^.sName := sName;]]></ST>
      </Implementation>
    </Method>
    <Property Name="HomeMode" Id="{6fbba694-14cc-47d7-b386-b3a87f93d4ab}">
      <Declaration><![CDATA[PROPERTY HomeMode : E_EpicsHomeCmd
]]></Declaration>
      <Get Name="Get" Id="{43254037-02b5-4fe5-96e1-8431d24f54f5}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[HomeMode := THIS^.fbHomeNC.Mode;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{1a6336ac-7eec-4dce-ac7d-8543b797a82e}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[THIS^.fbHomeNC.Mode := HomeMode;]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>