<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_MotionStageMCS2" Id="{18b2100b-a77e-4df0-a3a6-620c5efc7102}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'call_after_init'}
FUNCTION_BLOCK FB_MotionStageMCS2
VAR
    iMotionLogger : I_MotionLogger;
    {attribute 'no_copy'}
    AxisRef : REFERENCE TO AXIS_REF;
    {attribute 'no_copy'}
    stMotionEpicsItf : REFERENCE TO ST_MotionEpicsItf;
    iPersistentDataStorage : I_PersistentDataStorage;

    //Brakes : brake fb participate in sync comm with fbMotionDrive, fbMotionAxis and fbMotionInterlocks
    fbBrake : FB_Brake := (BrakeMode := E_StageBrakeMode.IF_MOVING);
    // common interlocks
    fbMotionInterlocksCommon : FB_MotionInterlocksCommon(iMotionLogger := iMotionLogger);
    // stage with Limit switches
    fbLimSwStatus : FB_LimSwStatusMCS2();
    fbMotionInterlocksLimSw : FB_MotionInterlocksLimSw ( iMotionInterlocksCommon:=fbMotionInterlocksCommon, iLimSwStatus:=fbLimSwStatus);
    fbHome : FB_HomeDS402(iMotionLogger:=iMotionLogger);
    //fbHomeNC : FB_HomeNC(AxisRef:=fbAxisRef, iMotionLogger:=iMotionLogger, iLimSwStatus:=fbLimSwStatus);;
    fbHalt : FB_HaltNC(AxisRef:=AxisRef);
    fbPower : FB_PowerNC(AxisRef:=AxisRef);
    fbReset : FB_ResetNC(AxisRef:=AxisRef);
    fbMoveAbsolute : FB_MoveAbsoluteNC(AxisRef:=AxisRef);
    fbAxisStatus : FB_AxisStatusNC(AxisRef:=AxisRef);
    fbReadParameter : FB_ReadParameterNC(AxisRef:=AxisRef);
    fbWriteParameter  : FB_WriteParameterNC(AxisRef:=AxisRef);
    fbParamsSaveRestore: FB_ParamsSaveRestoreNC(AxisRef:=AxisRef, iPersistentDataStorage := iPersistentDataStorage);
    fbEncoderScaling : FB_EncoderScaling;
    fbBacklashCompensation : FB_BacklashCompensationNC(AxisRef:=AxisRef);
    (*The axis object has a interlock reference in this instance: this would make calling method
      to transfer power status from interlock interface override the internal passing of power enable status
      the powerenables() is provided in case the interlock status were passed via the drive
      nonetheless passing via this object is more flexible
    *)
    fbMotionAxis : FB_MotionAxisMCS2( 	iHome := fbHome,
                                        iHalt := fbHalt,
                                        iPower := fbPower,
                                        iReset := fbReset,
                                        iMoveAbsolute := fbMoveAbsolute,
                                        iReadParameter := fbReadParameter,
                                        iWriteParameter  := fbWriteParameter,
                                        iParamSaveRestore:= fbParamsSaveRestore,
                                        iEncoderScaling := fbEncoderScaling,
                                        iAxisStatus := fbAxisStatus,
                                        iBacklashCompensation :=fbBacklashCompensation,
                                        iLimSwStatus:=fbLimSwStatus
                                    );
  //
   fbMotionDrive: FB_MotionDrive ( stMotionEpicsItf := stMotionEpicsItf,
                                   iBrake:=fbBrake,
                                   iMotionLogger:=iMotionLogger,
                                   iMotionInterlocks:=fbMotionInterlocksLimSw,
                                   iMotionAxis := fbMotionAxis
                                   ) ;
    // Drive Name
    sName : T_MaxString;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
fbMotionInterlocksCommon();
fbLimSwStatus();
fbMotionInterlocksLimSw();
fbReset();
fbHalt();
fbPower();
fbHome();
fbBacklashCompensation();
fbMoveAbsolute();
fbReadParameter();
fbWriteParameter();
fbParamsSaveRestore();
fbAxisStatus();
fbMotionAxis();
fbMotionDrive();
fbEncoderScaling();
fbBrake();
// fbMotionLogger();
// fbPersistentDataStorage();]]></ST>
    </Implementation>
    <Property Name="BrakeMode" Id="{32b8cf04-7ff9-4aa5-9f6f-6f32638c4c47}">
      <Declaration><![CDATA[{warning 'Add property implementation'}
PROPERTY BrakeMode : E_StageBrakeMode
]]></Declaration>
      <Get Name="Get" Id="{089a00cd-2a50-442b-997e-c6cb474b823c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[BrakeMode := fbBrake.BrakeMode;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{38f9611d-e6a8-4c93-938d-ded49fb62bf1}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[fbBrake.BrakeMode := BrakeMode;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="CallAfterInit" Id="{207f46b0-ff72-424e-9066-520bd8603492}">
      <Declaration><![CDATA[{attribute 'call_after_init'}
METHOD PRIVATE CallAfterInit]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbMotionInterlocksCommon.CallAfterInit(iMotionLogger := THIS^.iMotionLogger);
//fbLimSwStatus.CallAfterInit(iMotionLogger := THIS^.iMotionLogger);
fbMotionInterlocksLimSw.CallAfterInit(iMotionInterlocksCommon := THIS^.fbMotionInterlocksCommon,
                                      iLimSwStatus := THIS^.fbLimSwStatus);
fbHome.CallAfterInit( iMotionLogger := THIS^.iMotionLogger);
fbHalt.CallAfterInit(AxisRef := THIS^.AxisRef);
fbPower.CallAfterInit(AxisRef := THIS^.AxisRef);
fbReset.CallAfterInit(AxisRef := THIS^.AxisRef);
fbMoveAbsolute.CallAfterInit(AxisRef := THIS^.AxisRef);
fbParamsSaveRestore.CallAfterInit(AxisRef := THIS^.AxisRef,iPersistentDataStorage := THIS^.iPersistentDataStorage);
fbAxisStatus.CallAfterInit(AxisRef := THIS^.AxisRef);
fbBacklashCompensation.CallAfterInit(AxisRef := THIS^.AxisRef);

fbMotionAxis.CallAfterInit(iHome := THIS^.fbHome,
                             iHalt := THIS^.fbHalt,
                             iPower := THIS^.fbPower,
                             iReset := THIS^.fbReset,
                             iMoveAbsolute := THIS^.fbMoveAbsolute,
                             iReadParameter := THIS^.fbReadParameter,
                             iWriteParameter  := THIS^.fbWriteParameter,
                             iParamSaveRestore:= THIS^.fbParamsSaveRestore,
                             iEncoderScaling := THIS^.fbEncoderScaling,
                             iAxisStatus := THIS^.fbAxisStatus,
                             iBacklashCompensation := THIS^.fbBacklashCompensation,
                             iLimSwStatus := THIS^.fbLimSwStatus);

fbMotionDrive.CallAfterInit( stMotionEpicsItf:= THIS^.stMotionEpicsItf,
                             iBrake:=THIS^.fbBrake,
                             iMotionInterlocks:=THIS^.fbMotionInterlocksLimSw,
                             iMotionLogger:=THIS^.iMotionLogger,
                             iMotionAxis:=THIS^.fbMotionAxis );

THIS^.stMotionEpicsItf.sName := THIS^.sName;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="EnableMode" Id="{2cfee4fc-e5e8-4c8e-b6b7-a59e7c132daa}">
      <Declaration><![CDATA[PROPERTY EnableMode : ENUM_StageEnableMode]]></Declaration>
      <Get Name="Get" Id="{aea1400b-2ce4-4b67-86c1-ab83d3783067}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[EnableMode := THIS^.fbMotionDrive.EnableMode;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{535eda7b-2bf1-4638-b4f2-2b0c581a8459}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[THIS^.fbMotionDrive.EnableMode := EnableMode;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="FB_Init" Id="{c628d036-2c0d-41e6-9038-33393210aee4}">
      <Declaration><![CDATA[//FB_Init is always available implicitly and it is used primarily for initialization.
//The return value is not evaluated. For a specific influence, you can also declare the
//methods explicitly and provide additional code there with the standard initialization
//code. You can evaluate the return value.
METHOD FB_Init: BOOL
VAR_INPUT
    bInitRetains: BOOL; // TRUE: the retain variables are initialized (reset warm / reset cold)
    bInCopyCode: BOOL;  // TRUE: the instance will be copied to the copy code afterward (online change)
    iMotionLogger : I_MotionLogger;
    AxisRef : REFERENCE TO AXIS_REF;
    stMotionEpicsItf : REFERENCE TO ST_MotionEpicsItf;
    iPersistentDataStorage : I_PersistentDataStorage;
    sName : T_MaxString;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.iMotionLogger := iMotionLogger;
THIS^.AxisRef REF= AxisRef;
THIS^.iPersistentDataStorage := iPersistentDataStorage;
THIS^.stMotionEpicsItf REF=stMotionEpicsItf;
THIS^.sName := sName;]]></ST>
      </Implementation>
    </Method>
    <Property Name="HomeMode" Id="{4292331e-f2e7-4389-9c71-a8c481548aa0}">
      <Declaration><![CDATA[PROPERTY HomeMode : E_EpicsHomeCmd
]]></Declaration>
      <Get Name="Get" Id="{6f87226d-ca7d-452e-a79d-0e86ba0b660c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[HomeMode := THIS^.fbHome.Mode;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{41a97657-9a9a-4018-afcb-93d7bab125f4}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[THIS^.fbHome.Mode := HomeMode;]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>